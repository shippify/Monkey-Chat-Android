apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.Criptext'
version = '2.0.0'

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    useLibrary 'org.apache.http.legacy'
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.robolectric:robolectric:3.2'
    testImplementation 'org.robolectric:shadows-support-v4:3.0'
    testImplementation 'org.amshove.kluent:kluent:1.13'
    //robolectric needs this for API 23 https://github.com/robolectric/robolectric/issues/1932
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'com.github.kittinunf.result:result:1.0.5'
    api 'com.google.android.gms:play-services-gcm:11.4.0'
    api 'com.squareup.okhttp3:okhttp:3.3.1'
    api 'com.android.support:appcompat-v7:26.+'
    api 'com.android.support:design:26.+'
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

